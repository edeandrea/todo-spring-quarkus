plugins {
	id 'java'
	id 'io.quarkus' version '2.6.3.Final'
}

apply from: 'build-common.gradle'

ext {
	quarkusVersion = '2.6.3.Final'
	springVersion = '2.6.3'
}

compileJava {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-parameters'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

dependencies {
	// This dependency is a hack for TodoApplication.java, which isn't required for Quarkus. Point of demo is to NOT have any code changes.
	// It won't actually be included in the binary that gets built (compileOnly configuration)
	compileOnly "org.springframework.boot:spring-boot-autoconfigure:$springVersion"
	implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:$quarkusVersion")
	implementation 'io.quarkus:quarkus-spring-web'
	implementation 'io.quarkus:quarkus-spring-data-jpa'
	implementation 'io.quarkus:quarkus-spring-boot-properties'
	implementation 'io.quarkus:quarkus-smallrye-openapi'
	implementation 'io.quarkus:quarkus-smallrye-health'
	implementation 'io.quarkus:quarkus-openshift'
	implementation 'io.quarkus:quarkus-micrometer-registry-prometheus'
	implementation 'io.quarkus:quarkus-jdbc-postgresql'
	implementation 'io.quarkus:quarkus-resteasy-reactive-jackson'
	testImplementation 'io.quarkus:quarkus-junit5'
	testImplementation 'io.quarkus:quarkus-junit5-mockito'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'org.assertj:assertj-core'
}

quarkusBuild {
	nativeArgs {
		containerBuild = true
		buildImage = "quay.io/quarkus/ubi-quarkus-native-image:21.3.0-java11"
	}
}

task bootRun(dependsOn: 'quarkusDev')
